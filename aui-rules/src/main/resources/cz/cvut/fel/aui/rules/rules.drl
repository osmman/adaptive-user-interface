package cz.cvut.fel.aui.rules;

import cz.cvut.fel.aui.model.context.Age;
import cz.cvut.fel.aui.model.context.Device;
import java.util.List
import java.util.Locale
import java.awt.ComponentOrientation;

global java.util.Map env;


rule "Country" when
    cz.cvut.fel.aui.model.Context($country:country)
then
    env.put("country",$country);
end



rule "Show data as list" when
    cz.cvut.fel.aui.model.Context(device != Device.DESKTOP)
then
    env.put("table","list");
end



rule "Locale" when
    cz.cvut.fel.aui.model.Context($language:language, $country:country)
then
    Locale locale = new Locale($language, $country);
    env.put("locale", new Locale($language, $country));
    insert(locale);
end



rule "Right to left" when
    $locale: Locale()
then
    env.put("RTL",!ComponentOrientation.getOrientation($locale).isLeftToRight());
end



rule "Mobile interface" when
    cz.cvut.fel.aui.model.Context(device != Device.DESKTOP)
then
    env.put("layout","mobile");
end




rule "Graphic ui" when
    cz.cvut.fel.aui.model.Context(age == Age.CHILD)
then
    env.put("applyImage",true);
end



rule "Enable help" when
    cz.cvut.fel.aui.model.Context(age == Age.ELDER || invalid > 2)
then
    env.put("applyHelp",true);
end



rule "AspectFaces roles" when
    cz.cvut.fel.aui.model.Context($age:age)
    ctx: com.codingcrayons.aspectfaces.configuration.Context()
then
    ctx.setRoles(new String[]{$age.name().toLowerCase()});
end



rule "AspectFaces profile COUNTRY_%" when
    cz.cvut.fel.aui.model.Context($country:country)
    ctx: com.codingcrayons.aspectfaces.configuration.Context()
then
    ctx.setProfiles(new String[]{"COUNTRY_"+$country});
end



rule "AspectFaces mobile layout" when
    cz.cvut.fel.aui.model.Context(device != Device.DESKTOP)
    ctx: com.codingcrayons.aspectfaces.configuration.Context()
then
    ctx.setLayout("mobile");
end